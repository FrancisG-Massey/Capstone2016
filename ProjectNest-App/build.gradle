buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.0.8'
    }
}

apply plugin: 'org.javafxports.jfxmobile'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply plugin: 'jdepend'

task jdependReport(dependsOn: jdependMain, group: 'verification', description: 'Generates a HTML report after executing jdepend checks.') << {
   ant.xslt(
      in: "$reporting.baseDir/jdepend/main.xml",
      style: "$rootDir/conf/jdepend/jdepend-report.xslt",
      out:"$reporting.baseDir/jdepend/main.html")
}

check {
   dependsOn += ['jdependReport']
}

eclipse {
	classpath {
		downloadSources = true
     	downloadJavadoc = true
   	}
}

def hasGluonLogin = false

repositories {
    jcenter()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
    }
    maven {
       url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    if (project.hasProperty('gluonNexusUsername') && project.hasProperty('gluonNexusPassword')) {
    	hasGluonLogin = true
	    maven {
	    	url 'http://nexus.gluonhq.com/nexus/content/repositories/gluon-oss-daily/' 
		  	credentials { 
		    	username gluonNexusUsername
		    	password gluonNexusPassword
		  	} 
		}
	    maven {
	    	url 'http://nexus.gluonhq.com/nexus/content/repositories/gluon-private-releases/' 
		  	credentials { 
		    	username gluonNexusUsername
		    	password gluonNexusPassword
		  	} 
		}
	}
}

mainClassName = 'org.nestnz.app.NestApplication'

dependencies {
    compile 'com.gluonhq:charm-glisten:3.0.1-b7'
    compile 'com.gluonhq:connect:1.0.2-SNAPSHOT'
    compile 'com.gluonhq:maps:1.0.1-b5'
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'

    androidRuntime 'com.gluonhq:charm-glisten-android:3.0.1-b7'
    
    iosRuntime 'com.gluonhq:charm-glisten-ios:3.0.1-b7'
    
    desktopRuntime 'com.gluonhq:charm-glisten-desktop:3.0.1-b7'
    
    //charm-down-core
    compile 'com.gluonhq:down-core:3.0.1-b1'
    androidRuntime 'com.gluonhq:down-core-android:3.0.1-b1'
    iosCompile 'com.gluonhq:down-core-ios:3.0.1-b1'
    
    //position
    compile 'com.gluonhq:down-plugin-position:3.0.1-b1'
    androidRuntime 'com.gluonhq:down-plugin-position-android:3.0.1-b1'
    iosRuntime 'com.gluonhq:down-plugin-position-ios:3.0.1-b1'
    
    //settings
    compile 'com.gluonhq:down-plugin-settings:3.0.1-b1'
    androidRuntime 'com.gluonhq:down-plugin-settings-android:3.0.1-b1'
    iosRuntime 'com.gluonhq:down-plugin-settings-ios:3.0.1-b1'
    desktopRuntime 'com.gluonhq:down-plugin-settings-desktop:3.0.1-b1'
    
    //storage
    compile 'com.gluonhq:down-plugin-storage:3.0.1-b1'
    androidRuntime 'com.gluonhq:down-plugin-storage-android:3.0.1-b1'
    iosRuntime 'com.gluonhq:down-plugin-storage-ios:3.0.1-b1'
    desktopRuntime 'com.gluonhq:down-plugin-storage-desktop:3.0.1-b1'
    
    //vibration
    compile 'com.gluonhq:down-plugin-vibration:3.0.1-b1'
    androidRuntime 'com.gluonhq:down-plugin-vibration-android:3.0.1-b1'
    iosRuntime 'com.gluonhq:down-plugin-vibration-ios:3.0.1-b1'
    
}

jfxmobile {
	/*downConfig {
        version '3.0.0-SNAPSHOT'
        plugins 'position', 'settings', 'storage', 'vibration'
    }*/

	javafxportsVersion = hasGluonLogin ? '8.60.d161002' : '8.60.7'

    android {
        manifest = 'src/android/AndroidManifest.xml'
        
	    dexOptions {
	        incremental true
	        preDexLibraries = false
	        jumboMode = false
	        javaMaxHeapSize "2048M"
	    }
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.gluonhq.**.*',
                'io.datafx.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*',
                'org.nestnz.app.ios.**.*'
        ]
    }
}

retrolambda {
  jdk System.getenv("JAVA8_HOME")
  oldJdk System.getenv("JAVA7_HOME")
  javaVersion JavaVersion.VERSION_1_7
  defaultMethods false
  incremental true
}

//Stop the "test" task from using oldJdk (which doesn't include JavaFX)
project.tasks.getByName("test").doFirst {
	executable "java"
}
