buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.1.1'
    }
}

apply plugin: 'org.javafxports.jfxmobile'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply plugin: 'jdepend'
apply plugin: 'jacoco'

task jdependReport(dependsOn: jdependMain, group: 'verification', description: 'Generates a HTML report after executing jdepend checks.') << {
   ant.xslt(
      in: "$reporting.baseDir/jdepend/main.xml",
      style: "$rootDir/conf/jdepend/jdepend-report.xslt",
      out:"$reporting.baseDir/jdepend/main.html")
}

check {
   dependsOn += ['jdependReport']
}

eclipse {
	classpath {
		downloadSources = true
     	downloadJavadoc = true
   	}
}

def hasGluonLogin = false

repositories {
	mavenLocal()
    jcenter()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
    }
    maven {
       url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

mainClassName = 'org.nestnz.app.NestApplication'

dependencies {
    compile 'com.gluonhq:charm-glisten:4.0.1'
    compile 'com.gluonhq:connect:1.1.0'
    compile 'com.gluonhq:maps:1.0.1'
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'  
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

jfxmobile {
	downConfig {
        version '3.0.0'
        plugins 'position', 'settings', 'storage', 'vibration'
    }

    android {
        manifest = 'src/android/AndroidManifest.xml'
        
        minSdkVersion = 19
        targetSdkVersion = 22
        
	    dexOptions {
	        incremental true
	        preDexLibraries = false
	        jumboMode = false
	        javaMaxHeapSize "2048M"
	    }
	    
	    signingConfig {
	    	keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
	    }
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.gluonhq.**.*',
                'io.datafx.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*',
                'org.nestnz.app.ios.**.*'
        ]
    }
}
